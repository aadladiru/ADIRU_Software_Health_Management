package VD_Module
public
with ARINC653;
with SHM_DataType;
with SEI;
with Memory_Properties;
with ADIRU_errLibrary;

system voter_display
features
  ADIRU1_data: in data port SHM_DataType::accData;
  ADIRU2_data: in data port SHM_DataType::accData;
  ADIRU3_data: in data port SHM_DataType::accData;
  ADIRU4_data: in data port SHM_DataType::accData;
 
  voterL_error: out event data port SHM_DataType::errorData;
  voterC_error: out event data port SHM_DataType::errorData;
  voterR_error: out event data port SHM_DataType::errorData;

  displayL_error: out event data port SHM_DataType::errorData;
  displayC_error: out event data port SHM_DataType::errorData;
  displayR_error: out event data port SHM_DataType::errorData;

  voterL_errorAction: in event data port SHM_DataType::actionData;
  voterC_errorAction: in event data port SHM_DataType::actionData;
  voterR_errorAction: in event data port SHM_DataType::actionData;
  
  displayL_errorAction: in event data port SHM_DataType::actionData;
  displayC_errorAction: in event data port SHM_DataType::actionData;
  displayR_errorAction: in event data port SHM_DataType::actionData;
end voter_display;

system implementation voter_display.impl
annex EMV2{**
    use types ADIRU_errLibrary;
    use behavior ADIRU_errLibrary::display_simple;
    
    error propagations
      ADIRU1_data: in propagation{ValueErroneous};
      ADIRU2_data: in propagation{ValueErroneous};
      ADIRU3_data: in propagation{ValueErroneous};
      ADIRU4_data: in propagation{ValueErroneous};
    flows
      f1 : error sink ADIRU1_data{ValueErroneous};
      f2 : error sink ADIRU2_data{ValueErroneous};
      f3 : error sink ADIRU3_data{ValueErroneous};
      f4 : error sink ADIRU4_data{ValueErroneous};
    end propagations;

    component error behavior
    transitions
      t1 : operational -[ (ADIRU1_data{ValueErroneous} and ADIRU2_data{ValueErroneous} and ADIRU3_data{ValueErroneous}) or
                          (ADIRU1_data{ValueErroneous} and ADIRU2_data{ValueErroneous} and ADIRU4_data{ValueErroneous}) or
                          (ADIRU1_data{ValueErroneous} and ADIRU3_data{ValueErroneous} and ADIRU4_data{ValueErroneous}) or
                          (ADIRU2_data{ValueErroneous} and ADIRU3_data{ValueErroneous} and ADIRU4_data{ValueErroneous})
                        ]-> displayErroneousValue;
    end component;
**};
end voter_display.impl;

end VD_Module;
