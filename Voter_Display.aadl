package VD_Module
public
with ARINC653;
with SHM_DataType;
with SEI;
with Memory_Properties;
with ADIRU_errLibrary;

system voter_display
features
  ADIRU_input1: in data port SHM_DataType::accData;
  ADIRU_input2: in data port SHM_DataType::accData;
  ADIRU_input3: in data port SHM_DataType::accData;
  ADIRU_input4: in data port SHM_DataType::accData;
 
  voterL_error_out: out event data port SHM_DataType::errorData;
  voterL_error_action: in event data port SHM_DataType::actionData;
  displayL_error_out: out event data port SHM_DataType::errorData;
  displayL_error_action: in event data port SHM_DataType::actionData;
  
  voterC_error_out: out event data port SHM_DataType::errorData;
  voterC_error_action: in event data port SHM_DataType::actionData;
  displayC_error_out: out event data port SHM_DataType::errorData;
  displayC_error_action: in event data port SHM_DataType::actionData;
  
  voterR_error_out: out event data port SHM_DataType::errorData;
  voterR_error_action: in event data port SHM_DataType::actionData;
  displayR_error_out: out event data port SHM_DataType::errorData;
  displayR_error_action: in event data port SHM_DataType::actionData;
end voter_display;

system implementation voter_display.impl
annex EMV2{**
    use types ADIRU_errLibrary;
    use behavior ADIRU_errLibrary::display_simple;
    
    error propagations
      ADIRU_input1: in propagation{ValueError};
      ADIRU_input2: in propagation{ValueError};
      ADIRU_input3: in propagation{ValueError};
      ADIRU_input4: in propagation{ValueError};
    flows
      f1 : error sink ADIRU_input1{ValueError};
      f2 : error sink ADIRU_input2{ValueError};
      f3 : error sink ADIRU_input3{ValueError};
      f4 : error sink ADIRU_input4{ValueError};
    end propagations;

    component error behavior
    transitions
      t1 : operational -[ (ADIRU_input1{ValueError} and ADIRU_input2{ValueError} and ADIRU_input3{ValueError}) or
                          (ADIRU_input1{ValueError} and ADIRU_input2{ValueError} and ADIRU_input4{ValueError}) or
                          (ADIRU_input1{ValueError} and ADIRU_input3{ValueError} and ADIRU_input4{ValueError}) or
                          (ADIRU_input2{ValueError} and ADIRU_input3{ValueError} and ADIRU_input4{ValueError})
                        ]-> displayErroneousValue;
    end component;
**};
end voter_display.impl;

end VD_Module;
